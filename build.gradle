import com.bmuschko.gradle.cargo.convention.Deployable
import com.bmuschko.gradle.cargo.tasks.remote.CargoRedeployRemote

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.9.0'
    }
}

plugins {
    id 'java'
    id 'war'
}

apply plugin: 'com.bmuschko.cargo-base'

compileJava.options.encoding = 'UTF-8'

group = 'raubach.frickl.next'
version '2.0.0'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'lib', include: ['*.jar'])

    implementation 'commons-io:commons-io:2.11.0'

    implementation 'org.json:json:20220924'
    implementation 'com.google.code.gson:gson:2.10'

    implementation 'mysql:mysql-connector-java:8.0.31'

    implementation 'org.jooq:jooq:3.16.18'
    implementation 'org.jooq:jooq-codegen:3.16.18'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.2'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'

    implementation 'org.flywaydb:flyway-core:6.5.7'

    implementation 'net.coobird:thumbnailator:0.4.17'

    providedCompile 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    implementation 'com.drewnoakes:metadata-extractor:2.17.0'
    implementation 'com.adobe.xmp:xmpcore:6.1.11'

    implementation 'org.glassfish.jersey.containers:jersey-container-servlet:3.1.1'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.1.1'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart:3.1.1'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

// Generate a .war file
war {
    dependsOn jar

    rootSpec.exclude('**/jhi/**/*.class')
    rootSpec.includeEmptyDirs = false

    // Include external .jar files, but exclude the Germinate client and core.
    classpath fileTree(dir:'build/libs/', include:'*.jar')

    // Set the classpath
    manifest {
        attributes 'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '),
                'Implementation-Title': 'GridScore',
                'Implementation-Version': archiveVersion
    }

    webInf {
        // Include the .properties file into the classes folder
        from(project.projectDir.toString()) {
            include 'config.properties'
            include 'logging.properties'
            into('classes')
        }
    }

    // Include the client code if it's available
    from("${project.projectDir.toString()}/client") {
        include '**/**.*'
        into('/')
    }
}

// Runs the JOOQ code generation
task codegen (type: JavaExec) {
    group = 'Execution'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.jooq.codegen.GenerationTool'
    args 'jooq.xml'
    finalizedBy build
}

// Deploy the created .war file to Tomcat
task deployTomcat (type: CargoRedeployRemote) {
    dependsOn = [war]
    containerId = project.findProperty('tomcat.manager.version') ?: "tomcat10x"
    protocol = project.findProperty('tomcat.manager.protocol') ?: "http"
    hostname = project.findProperty('tomcat.manager.hostname') ?: "localhost"
    port = (project.findProperty('tomcat.manager.port') ?: "8080") as Integer
    username = project.findProperty('tomcat.manager.username') ?: ""
    password = project.findProperty('tomcat.manager.password') ?: ""
    deployables = [new Deployable(files: project.files([war.archiveFile]), context: "${project.'project.name'}/v${project.version}")]
}